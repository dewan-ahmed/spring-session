buildscript {
	ext {
		releaseBuild = version.endsWith('RELEASE')
		snapshotBuild = version.endsWith('SNAPSHOT')
		milestoneBuild = !(releaseBuild || snapshotBuild)
	}

	repositories {
		gradlePluginPortal()
		mavenCentral()
		maven { url 'https://repo.spring.io/plugins-release/' }
        maven {
            url = 'https://repo.spring.io/plugins-snapshot'
            if (project.hasProperty('artifactoryUsername')) {
                credentials {
                    username "$artifactoryUsername"
                    password "$artifactoryPassword"
                }
            }
        }
	}

	dependencies {
		classpath libs.org.springframework.boot.spring.boot.gradle.plugin
		classpath 'io.harness:gradle-cache:0.0.4'
	}
}

plugins {
	id "com.github.ben-manes.versions"
    	id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.2'
}

apply plugin: 'io.spring.convention.root'
apply plugin: 'io.spring.security.release'
apply plugin: 'io.harness.gradle-cache'

buildCache {
    local {
        // Local build cache is dangerous as it might produce inconsistent results
        // in case developer modifies files while the build is running
        enabled = false
    }
    remote(io.harness.Cache) {
        push = System.getenv('CI') == "true"
    }
}

group = 'org.springframework.session'
description = 'Spring Session'

subprojects {
	apply plugin: 'io.spring.javaformat'

	plugins.withType(JavaPlugin) {
		sourceCompatibility = JavaVersion.VERSION_17
	}

	tasks.withType(Test) {
		useJUnitPlatform()
	}

	// Spring Framework 6.1 requires -parameters to be able to introspect method parameter names
	tasks.withType(JavaCompile) {
		options.compilerArgs.add("-parameters")
	}
}

nohttp {
    source.exclude "buildSrc/build/**"
	source.exclude "spring-session-docs/.gradle/nodejs/**"
	source.exclude "spring-session-docs/modules/ROOT/examples/**/build/**"
}

springRelease {
	repositoryOwner = "spring-projects"
	repositoryName = "spring-session"
	weekOfMonth = 3
	dayOfWeek = 2
	referenceDocUrl = "https://docs.spring.io/spring-session/reference/{version}/index.html"
	apiDocUrl = "https://docs.spring.io/spring-session/docs/{version}/api/"
	replaceSnapshotVersionInReferenceDocUrl = true
}
